#!/usr/bin/env tclsh
# 
# Server for remote SKILL script execution
# Based on design by A.D.Beckett (Cadence Design Systems Ltd)
# 
# Opens a server socket and listens for incoming connections.
# When data comes in, it passes it to DFII via stdout.
# DFII sends results back via this process's stdin.
#

if [info exists env(SKILLSERVPORT)] {
  set port $env(SKILLSERVPORT)
} else {
  set port 8123
}

proc listener {channel addr port} {
  puts "$channel abSkillServerConnection(\"$addr\" \"$port\")"
  flush stdout
  fconfigure $channel -buffering line
  fileevent $channel readable [list sendToDFII $channel]
}

proc sendToDFII {channel} {
  if {[eof $channel] || [catch {gets $channel line}]} {
    # end of file
    close $channel
  } else {
    puts "$channel $line"
    flush stdout
  }
}
  
proc sendBack {} {
  gets stdin line
  regsub {^(\w+) .*$} $line {\1} channel
  regsub {^\w+ (.*)$} $line {\1} result

  if {![eof $channel]} {
    puts $channel $result
  }
}

puts "SKILL Server starting on port $port..."
fconfigure stdin -buffering line
fileevent stdin readable sendBack

socket -server listener $port
vwait forever
